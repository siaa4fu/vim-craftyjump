scriptencoding utf-8
" the text 'themis#suite()' is required to run test#viml#themis#test_file()
Describe word-motions

let Expect = function(themis#helper('expect'))

function! s:init_test_buffer()
  new | only!
  let lines = readfile('test/sample_text')
  for lnum in range(1, len(lines))
    call setline(lnum, lines[lnum - 1])
  endfor
endfunction

function! s:getKwdIndices(line, motion)
  " @param {string} line
  " @param {'w' | 'b' | 'e' | 'ge'} motion
  "   'w'  - get start positions of matches and return their indices
  "   'b'  - get start positions of matches and return their indices reversed
  "   'e'  - get end positions of matches and return their indices
  "   'ge' - get end positions of matches and return their indices reversed
  " @return {list<number>} - character indices of keyword characters
  let indices = []
  let Put = function(a:motion ==# 'w' || a:motion ==# 'e' ? 'add' : 'insert', [indices])
  let pos = matchstrpos(a:line, '\k\+') " 0-based byte index
  if a:motion ==# 'w' || a:motion ==# 'b'
    while pos[0] !=# ''
      " matchstrpos(...)[1] gives the 0-based byte index of the starting position of the match
      call Put(charidx(a:line, pos[1]) + 1) " convert 0-based character index to 1-based character index
      let pos = matchstrpos(a:line, '\k\+', pos[2])
    endwhile
  else
    while pos[0] !=# ''
      " matchstrpos(...)[2] gives the 0-based byte index of the first character AFTER the match
      call Put(charidx(a:line, pos[2])) " equal to 1-based character index
      let pos = matchstrpos(a:line, '\k\+', pos[2])
    endwhile
  endif
  return indices
endfunction

function! s:moveInLine(lnum, motion, indices) closure
  " @param {number} lnum - move in the line with the specified line number
  " @param {'w' | 'b' | 'e' | 'ge'} motion
  " @param {list<number>} indices - character indices of keyword characters
  for index in a:indices
    execute "normal \<Plug>(craftyjump-word-" . a:motion . ')'
    call Expect('.').to_be_at_position([a:lnum, index])
  endfor
endfunction

Context simple moving
  Before
    call s:init_test_buffer()
  End
  It moves forward to the start of word (w)
    " start moving from the first character of the line 1
    normal! gg0
    " line 1 (skip the initial position)
    call s:moveInLine(1, 'w', {indices ->
          \ get(indices, 0, -1) == 1 ? indices[1 :] : indices
          \}(s:getKwdIndices(getline(1), 'w')))
    " line 2
    call s:moveInLine(2, 'w', s:getKwdIndices(getline(2), 'w'))
    " extra move
    execute "normal \<Plug>(craftyjump-word-w)"
    call Expect('.').to_be_at_position([2, strcharlen(getline(2))])
  End
  It moves backward to the start of word (b)
    " start moving from the last character of the line 2
    normal! 2gg$
    " line 2 (skip the initial position)
    call s:moveInLine(2, 'b', {indices ->
          \ get(indices, 0, -1) == strcharlen(getline(2)) ? indices[1 :] : indices
          \}(s:getKwdIndices(getline(2), 'b')))
    " line 1
    call s:moveInLine(1, 'b', s:getKwdIndices(getline(1), 'b'))
    " extra move
    execute "normal \<Plug>(craftyjump-word-b)"
    call Expect('.').to_be_at_position([1, 1])
  End
  It moves forward to the end of word (e)
    " start moving from the first character of the line 1
    normal! gg0
    " line 1 (skip the initial position)
    call s:moveInLine(1, 'e', {indices ->
          \ get(indices, 0, -1) == 1 ? indices[1 :] : indices
          \}(s:getKwdIndices(getline(1), 'e')))
    " line 2
    call s:moveInLine(2, 'e', s:getKwdIndices(getline(2), 'e'))
    " extra move
    execute "normal \<Plug>(craftyjump-word-e)"
    call Expect('.').to_be_at_position([2, strcharlen(getline(2))])
  End
  It moves backward to the end of word (ge)
    " start moving from the last character of the line 2
    normal! 2gg$
    " line 2 (skip the initial position)
    call s:moveInLine(2, 'ge', {indices ->
          \ get(indices, 0, -1) == strcharlen(getline(2)) ? indices[1 :] : indices
          \}(s:getKwdIndices(getline(2), 'ge')))
    " line 1
    call s:moveInLine(1, 'ge', s:getKwdIndices(getline(1), 'ge'))
    " extra move
    execute "normal \<Plug>(craftyjump-word-ge)"
    call Expect('.').to_be_at_position([1, 1])
  End
End

End
